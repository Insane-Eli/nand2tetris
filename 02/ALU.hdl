// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
      // x zero / negate
      Mux16(a=x, b=false, sel = zx, out=w0);
      Not16(in=w0, out=w1);
      Mux16(a=w0, b=w1, sel=nx, out=w2);

      // y zero / negate
      Mux16(a=y, b=false, sel = zy, out=w3);
      Not16(in=w3, out=w4);
      Mux16(a=w3, b=w4, sel=ny, out=w5);

      // x + y OR x & y
      And16(a=w2, b=w5, out=andOut);
	  Add16(a=w2, b=w5, out=addOut);
	  Mux16(a=andOut, b=addOut, sel=f, out=w6);
		
      // negate the output?
	  Not16(in=w6, out=w7);
	  Mux16(a=w6, b=w7, sel=no, out=out, out[0..7]=w8, out[8..15]=w9, out[15]=ng);

      // output == 0 (zr)
      Or8Way(in=w8, out=w10);
      Or8Way(in=w9, out=w11);
      Or(a=w10, b=w11, out=zrOut);
      Not(in=zrOut, out=zr);

      // output < 0 (ng)
}